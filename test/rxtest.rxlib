rtest:
/* ---------------------------------------------------------------------
 * Tests rexx functions
 *   rc=rtest("c2d('a'x)","\== 10",5)
 * .................................... Created by PeterJ on 1. Oct 2020
 * ---------------------------------------------------------------------
 */
maxrc=0
signal on syntax name funcerr
parse arg func,tresult,tno
rc=0
if func='' | tresult='' | tno='' then do
   say "Invalid call to rtest:" func tresult tno
   return 8
end
parse upper var func name "(" .
s = left(name,8,' ') '- test' right(tno,3,' ')
call interpret_it
if rc<8 then s = s '.. PASS'
else if rc==8 then do
   if PrecisionCLC(tvalue,Tresult)=1 then do
       s = s '.. *WARN* - function "'||func||'" '
       s = s 'expected "'||reversed(tresult)||'" '
       s = s 'actual "'||tvalue||' difference not significant'
       rc=0
   end
   else do
      s = s '.. *FAIL* - function "'||func||'" '
      s = s 'expected "'||reversed(tresult)||'" '
      s = s 'actual "'||tvalue||'" HEX('|| c2x(tvalue) ||')'
   end
end
signal off syntax
say s
maxrc=max(maxrc,rc)
return maxrc
/* reverse expected */
reversed:
parse arg rev
retrev = ''
if substr(strip(rev),1,1)=="\" then
  retrev = substr(strip(rev),2,length(rev))
else if substr(strip(rev),1,1)=="=" then
  retrev = "\"||strip(rev)
retrev = translate(retrev,"<",">")
retrev = translate(retrev,">","<")
return retrev
/* interpret */
interpret_it:
  interpret 'tvalue='value(func)
  interpret 'if tvalue ' tresult 'then rc=8'
  return
/* ----- Check, if difference is not significant ---------------------*/
PrecisionCLC:
  parse arg isvalue,svalue
  svalue=filter(svalue," '\=><"'"')
  if datatype(svalue)<>'NUM' then return 0
  if datatype(isvalue)<>'NUM' then return 0
  prec=abs(isvalue-svalue)
  if prec>0.000000001 then return 0
return 1
/* ----- Error Exit, if function call terminates ---------------------*/
funcerr:
 signal off syntax
 s = s '.. *ERROR* - function "'||func||'" -' errortext(rc)
 maxrc=max(maxrc,8)
return 0